

1 REM:SNAKE (C) 2025 DAVID R. VAN WAGNER
2 REM:GITHUB.COM/DAVERVW
3 :
10 POKE 646, 1
20 POKE 36879, 12
25 PRINT CHR$(147);
26 FORI=1TO22:PRINT:NEXT
27 PRINTCHR$(18)"SNAKE(C)2025 DAVERVW";
28 PRINT " "CHR$(157)CHR$(148)" ";
29 PRINTCHR$(19)CHR$(17)CHR$(29)"ARROW KEYS Z/ TURN"
30 S=30*256:IFPEEK(56)>30THENS=4096
40 C=9.375*4096:IFPEEK(56)>30THENC=9.25*4096
50 B=81:H=87:D=86:F=90
55 SD=10
60 DIM S%(506):L=0:R=0
61 FORO=0TO505:POKEC+O,1:NEXT
62 TM=TI
63 DT=TI-TM:IFDT>=0ANDDT<90THEN63
64 PRINTCHR$(19)CHR$(17)"                      "
70 X=10:Y=11:DX=1:DY=0:N=X+Y*22
71 GOSUB300
73 :
74 UP$=CHR$(145)
75 LT$=CHR$(157)
76 RT$=CHR$(29)
77 DN$=CHR$(17)
78 :
79 GOTO 130
80 TM=TI
90 DT=TI-TM:IFDT>=0 AND DT<SD THEN 90
100 X=X+DX:Y=Y+DY
110 IF X<0 OR Y<0 OR X>21 OR Y>21 THEN POKE S+S%(R),D:GOTO200
111 N=X+Y*22
112 IF PEEK(S+N)=F THEN GOSUB 300:GOTO 125
113 IF PEEK(S+N)<>32 THEN POKE S+N,D:GOTO 200
120 POKE S+S%(L),32
121 L=L+1:IFL=507THENL=0
125 IF PEEK(S+S%(R))=H THEN POKE S+S%(R),B
126 R=R+1:IFR=507THENR=0
130 S%(R)=N
131 POKE S+S%(R),H
140 GETK$
150 IFK$<>UP$ANDK$<>LT$THEN170
151 IFDX=1THENDX=0:DY=-1:GOTO190
152 IFDY=1THENDX=1:DY=0:GOTO190
153 IFDX=-1THENDX=0:DY=1:GOTO190
154 IFDY=-1THENDX=-1:DY=0:GOTO190
170 IFK$<>DN$ANDK$<>RT$THEN190
171 IFDX=1THENDX=0:DY=1:GOTO190
172 IFDY=1THENDX=-1:DY=0:GOTO190
173 IFDX=-1THENDX=0:DY=-1:GOTO190
174 IFDY=-1THENDX=1:DY=0
190 GOTO 80
200 TM=TI:LN=R+1-L-507*(LN<0):PRINTCHR$(19)CHR$(18);LN;CHR$(157);" ";
201 GETK$:DT=TI-TM:IFDT>=0ANDDT<120THEN201
210 GETK$:IFK$=""THEN210
220 RUN
300 J=INT(RND(1)*22*22)
301 LN=R+1-L:IFLN<0THENLN=LN+507
302 IF(LN/2)=INT(LN/2)ANDSD>1THENSD=SD-1
310 IFPEEK(S+J)<>32THEN300
320 POKES+J,F
330 RETURN

READY.


