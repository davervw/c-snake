

1 REM:SNAKE (C) 2025 DAVID R. VAN WAGNER
2 REM:GITHUB.COM/DAVERVW
3 :
6 Q=RND(-TI)
10 PRINTCHR$(30)
11 W=40:T=25
25 PRINT CHR$(147);
26 FORI=1TOT-1:PRINT:NEXT
27 PRINTCHR$(18)"SNAKE COPYRIGHT (C) 2025 DAVERVW     ";CHR$(5);" ";
28 FORI=1TO2:PRINT CHR$(157)CHR$(148)" ";:NEXT
29 PRINTCHR$(19)CHR$(17)CHR$(29)CHR$(29)"ARROW KEYS Z/ TURN  RETURN=PAUSE"
30 PRINT CHR$(17)"    "CHR$(18)"OR JOYSTICK#2"
35 S=PEEK(648)*256
40 C=13.5*4096
50 B=81:H=87:D=86:F=90
55 SD=10:REM DELAY
56 SZ=W*(T-1)+1
60 DIM S%(SZ):L=0:R=0:CS=1
62 TM=TI
63 DT=TI-TM:IFDT>=0ANDDT<90THEN63
64 PRINTCHR$(19)CHR$(17);:FORI=1TOW-1:PRINT" ";:NEXT:PRINT:PRINT
65 FORI=1TOW-1:PRINT" ";:NEXT:PRINT
70 X=W/2-1:Y=INT(T/2):DX=1:DY=0:N=X+Y*W
71 GOSUB300
73 GOSUB400
74 UP$=CHR$(145)
75 LT$=CHR$(157)
76 RT$=CHR$(29)
77 DN$=CHR$(17)
78 GOTO 130
80 :
100 X=X+DX:Y=Y+DY
110 IF X<0 OR Y<0 OR X>=W OR Y>(T-2) THEN POKE S+S%(R),D:GOTO200
111 N=X+Y*W
112 IF PEEK(S+N)=F THEN GOSUB 300:R=R+1:GOSUB 400:R=R-1:CS=PEEK(C+N)AND15:GOTO 125
113 IF N<>S%(L) THEN IF PEEK(S+N)<>32 THEN POKE S+S%(R),D:GOTO 200
120 POKE S+S%(L),32
121 L=L+1:IFL>SZTHENL=0
125 IF PEEK(S+S%(R))=H THEN POKE S+S%(R),B
126 R=R+1:IFR>SZTHENR=0
130 S%(R)=N
131 POKE C+S%(R),CS:POKE S+S%(R),H
134 J=0:TM=TI
135 DT=TI-TM
136 P=(NOTPEEK(56320)AND63):IFP<>0THENJ=P
137 IFDT<SDTHENIFDT>=0THEN135
140 GETK$:IFK$<>""THEN150
141 IFJ=0THEN80
142 IFJ=1THENIFDX<>0ORDY<>1THENDX=0:DY=-1
143 IFJ=2THENIFDX<>0ORDY<>-1THENDX=0:DY=1
144 IFJ=8THENIFDX<>-1ORDY<>0THENDX=1:DY=0
145 IFJ=4THENIFDX<>1ORDY<>0THENDX=-1:DY=0
146 IFJ<>16 THEN 150
147 IFJ=16THENJ=NOTPEEK(56320)AND16:GOTO147
148 IFJ=0THENJ=NOTPEEK(56320)AND16:GOTO148
149 IFJ=16THENJ=NOTPEEK(56320)AND16:GOTO149
150 IFK$<>UP$ANDK$<>LT$ANDK$<>"Z"THEN170
151 IFDX=1THENDX=0:DY=-1:GOTO190
152 IFDY=1THENDX=1:DY=0:GOTO190
153 IFDX=-1THENDX=0:DY=1:GOTO190
154 IFDY=-1THENDX=-1:DY=0:GOTO190
170 IFK$<>DN$ANDK$<>RT$ANDK$<>"/"THEN180
171 IFDX=1THENDX=0:DY=1:GOTO190
172 IFDY=1THENDX=-1:DY=0:GOTO190
173 IFDX=-1THENDX=0:DY=-1:GOTO190
174 IFDY=-1THENDX=1:DY=0:GOTO190
180 IFK$<>CHR$(13)THEN190
181 GETK$:IFK$<>CHR$(13)THEN181
190 GOTO 80
200 TM=TI
201 GETK$:DT=TI-TM:IFDT>=0ANDDT<120THEN201
210 GETK$:IFK$=""AND(PEEK(56320)AND16)=16THEN210
220 RUN
300 J=INT(RND(1)*(T-1)*W)
301 LN=R+1-L:IFLN<0THENLN=LN+SZ+1
302 IF(LN/2)=INT(LN/2)ANDSD>1THENSD=SD-1
310 IFPEEK(S+J)<>32THEN300
321 C%=RND(1)*16:IFC%=(PEEK(53281)AND15)THEN321
322 IFC%=(PEEK(C+N)AND15)THEN321
323 IFC%=(PEEK(C+S%(L))AND15)THEN321
324 IFC%=(PEEK(C+S%(R))AND15)THEN321
328 POKEC+J,C%
329 POKES+J,F
330 RETURN
400 LN=R-L+1:IFLN<0THENLN=LN+SZ+1
410 LN$=MID$(STR$(LN),2)
420 IFLEN(LN$)<3THENLN$=" "+LN$:GOTO420
430 POKES+(T-1)*W+W-3,ASC(MID$(LN$,1,1))+128
440 POKES+(T-1)*W+W-2,ASC(MID$(LN$,2,1))+128
450 POKES+(T-1)*W+W-1,ASC(MID$(LN$,3,1))+128
460 RETURN

READY.


