

1 REM:SNAKE (C) 2025 DAVID R. VAN WAGNER
2 REM:GITHUB.COM/DAVERVW
3 :
6 Q=RND(-TI)
10 GOSUB 500:SZ=W*(T-1)+1:DIM S%(SZ)
25 PRINT CHR$(147);CHR$(142);
26 FORI=1TOT-1:PRINT:NEXT
27 PRINTCHR$(18)"SNAKE COPYRIGHT (C) 2025 DAVERVW";
28 IFPOS(0)<W-2THENPRINT" ";:GOTO28
29 FORI=1TO2:PRINT CHR$(157)CHR$(148)" ";:NEXT
30 PRINTCHR$(19)CHR$(17)CHR$(29)CHR$(29)"ARROW KEYS Z/ TURN"
31 PRINT CHR$(17)"    "CHR$(18)"OR JOYSTICK#2"
50 B=81:H=87:D=86:F=90
55 SD=10:REM DELAY
60 L=0:R=0
62 TM=TI
63 DT=TI-TM:IFDT>=0ANDDT<90THEN63
64 PRINTCHR$(19)CHR$(17);:FORI=1TOW-1:PRINT" ";:NEXT:PRINT:PRINT
65 FORI=1TOW-1:PRINT" ";:NEXT:PRINT
70 X=W/2-1:Y=INT(T/2):DX=1:DY=0:N=X+Y*W
71 GOSUB300
73 GOSUB400
74 UP$=CHR$(145)
75 LT$=CHR$(157)
76 RT$=CHR$(29)
77 DN$=CHR$(17)
78 GOTO 130
80 :
100 X=X+DX:Y=Y+DY
110 IF X<0 OR Y<0 OR X>=W OR Y>(T-2) THEN POKE S+S%(R),D:GOTO200
111 N=X+Y*W
112 IF PEEK(S+N)=F THEN GOSUB 300:R=R+1:GOSUB 400:R=R-1:GOTO 125
113 IF N<>S%(L) THEN IF PEEK(S+N)<>32 THEN POKE S+S%(R),D:GOTO 200
120 POKE S+S%(L),32
121 L=L+1:IFL>SZTHENL=0
125 IF PEEK(S+S%(R))=H THEN POKE S+S%(R),B
126 R=R+1:IFR>SZTHENR=0
130 S%(R)=N
131 POKE S+S%(R),H
134 TM=TI
135 DT=TI-TM
137 IFDT<SDTHENIFDT>=0THEN135
140 GETK$:IFK$<>""THEN150
141 GOTO 80
150 IFK$<>UP$ANDK$<>LT$ANDK$<>"Z"THEN170
151 IFDX=1THENDX=0:DY=-1:GOTO190
152 IFDY=1THENDX=1:DY=0:GOTO190
153 IFDX=-1THENDX=0:DY=1:GOTO190
154 IFDY=-1THENDX=-1:DY=0:GOTO190
170 IFK$<>DN$ANDK$<>RT$ANDK$<>"/"THEN190
171 IFDX=1THENDX=0:DY=1:GOTO190
172 IFDY=1THENDX=-1:DY=0:GOTO190
173 IFDX=-1THENDX=0:DY=-1:GOTO190
174 IFDY=-1THENDX=1:DY=0
190 GOTO 80
200 TM=TI
201 GETK$:DT=TI-TM:IFDT>=0ANDDT<120THEN201
210 GETK$:IFK$=""THEN210
220 GOTO 25
300 J=INT(RND(1)*(T-1)*W)
301 LN=R+1-L:IFLN<0THENLN=LN+SZ+1
302 IF(LN/2)=INT(LN/2)ANDSD>1THENSD=SD-1
310 IFPEEK(S+J)<>32THEN300
329 POKES+J,F
330 RETURN
400 LN=R-L+1:IFLN<0THENLN=LN+SZ+1
410 LN$=MID$(STR$(LN),2)
420 IFLEN(LN$)<3THENLN$=" "+LN$:GOTO420
430 POKES+(T-1)*W+W-3,ASC(MID$(LN$,1,1))+128
440 POKES+(T-1)*W+W-2,ASC(MID$(LN$,2,1))+128
450 POKES+(T-1)*W+W-1,ASC(MID$(LN$,3,1))+128
460 RETURN
500 PRINT CHR$(147);"WAIT":PRINT"..."
505 S=0
510 W0=ASC("W")-64
515 W1=ASC("A")-64
520 W2=ASC("I")-64
525 W3=ASC("T")-64
530 IFPEEK(S)=W0THENIFPEEK(S+1)=W1THENIFPEEK(S+2)=W2THENIFPEEK(S+3)=W3THEN540
535 S=S+256:GOTO 530
540 W=0
545 IFPEEK(S+W)<>46THENW=W+1:GOTO545
550 PRINT CHR$(147);
555 T=0
560 PRINT MID$(STR$(T),2);
565 IFPEEK(S)=48THENPRINT:T=T+1:GOTO560
570 PRINT CHR$(147);
575 PRINT W;T;S
580 RETURN

READY.


