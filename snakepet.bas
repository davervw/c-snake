

1 REM:SNAKE (C) 2025 DAVID R. VAN WAGNER
2 REM:GITHUB.COM/DAVERVW
3 :
6 Q=RND(-TI)
10 GOSUB500:SZ=W*(T-1)+1:DIMS%(SZ)
20 PRINTCHR$(147);CHR$(142);
21 FORI=1TOT-1:PRINTCHR$(18)" ":NEXT:FORI=0TOT-1:POKES+I*W-1,160:NEXT
22 C$=CHR$(18)+"SNAKE COPYRIGHT (C) 2025 DAVERVW"
23 IFW=22THENC$=CHR$(18)+"SNAKE(C)2025DAVERVW"
24 PRINTC$;
28 IFPOS(0)<W-1THENPRINT" ";:GOTO28
29 PRINTCHR$(157)CHR$(148)" ";
30 PRINTCHR$(19)CHR$(17)CHR$(29)CHR$(29)"ARROW KEYS Z/ TURN"
31 PRINT
32 PRINTCHR$(29)CHR$(29)CHR$(18)"RETURN=PAUSE"
50 B=81:H=87:D=86:F=90
55 SD=10
60 L=0:R=0
62 TM=TI
63 DT=TI-TM:IFDT>=0ANDDT<90THEN63
64 PRINTCHR$(19)CHR$(17)CHR$(29);:FORI=1TOW-2:PRINT" ";:NEXT:PRINT:PRINT
65 PRINTCHR$(29);:FORI=1TOW-2:PRINT" ";:NEXT:PRINT
66 FORI=1TOW-2:POKES+I,160:NEXT:IFC<>0THENGOSUB700
70 X=W/2-1:Y=INT(T/2):DX=1:DY=0:N=X+Y*W
71 GOSUB300
73 GOSUB400
74 UP$=CHR$(145)
75 LT$=CHR$(157)
76 RT$=CHR$(29)
77 DN$=CHR$(17)
78 GOTO130
80 :
100 X=X+DX:Y=Y+DY
110 IFX<0ORY<0ORX>=WORY>(T-2)THENPOKES+S%(R),D:GOTO200
111 N=X+Y*W
112 IFPEEK(S+N)=FTHENGOSUB300:R=R+1:GOSUB400:R=R-1:GOTO125
113 IFN<>S%(L)THENIFPEEK(S+N)<>32THENPOKES+S%(R),D:GOTO 200
120 POKES+S%(L),32
121 L=L+1:IFL>SZTHENL=0
125 IFPEEK(S+S%(R))=HTHENPOKE S+S%(R),B
126 R=R+1:IFR>SZTHENR=0
130 S%(R)=N
131 POKES+S%(R),H
134 TM=TI
135 DT=TI-TM
137 IFDT<SDTHENIFDT>=0THEN135
140 GETK$:IFK$<>""THEN150
141 GOTO80
150 IFK$<>UP$ANDK$<>LT$ANDK$<>"Z"THEN170
151 IFDX=1THENDX=0:DY=-1:GOTO190
152 IFDY=1THENDX=1:DY=0:GOTO190
153 IFDX=-1THENDX=0:DY=1:GOTO190
154 IFDY=-1THENDX=-1:DY=0:GOTO190
170 IFK$<>DN$ANDK$<>RT$ANDK$<>"/"THEN180
171 IFDX=1THENDX=0:DY=1:GOTO190
172 IFDY=1THENDX=-1:DY=0:GOTO190
173 IFDX=-1THENDX=0:DY=-1:GOTO190
174 IFDY=-1THENDX=1:DY=0:GOTO190
180 IFK$<>CHR$(13)THEN190
181 GETK$:IFK$<>CHR$(13)THEN181
190 GOTO80
200 TM=TI
201 GETK$:DT=TI-TM:IFDT>=0ANDDT<120THEN201
210 GETK$:IFK$=""THEN210
220 RUN
300 J=INT(RND(1)*(T-1)*W)
301 LN=R+1-L:IFLN<0THENLN=LN+SZ+1
302 IF(LN/2)=INT(LN/2)ANDSD>1THENSD=SD-1
310 IFPEEK(S+J)<>32THEN300
329 POKES+J,F
330 RETURN
400 LN=R-L+1:IFLN<0THENLN=LN+SZ+1
410 LN$=MID$(STR$(LN),2)
420 IFLEN(LN$)<3THENLN$=" "+LN$:GOTO420
430 POKES+(T-1)*W+W-3,ASC(MID$(LN$,1,1))+128
440 POKES+(T-1)*W+W-2,ASC(MID$(LN$,2,1))+128
450 POKES+(T-1)*W+W-1,ASC(MID$(LN$,3,1))+128
460 RETURN
500 PRINTCHR$(147);"WAIT":PRINT"..."
505 S=0
510 W0=ASC("W")-64
515 W1=ASC("A")-64
520 W2=ASC("I")-64
525 W3=ASC("T")-64
530 IFPEEK(S)=W0THENIFPEEK(S+1)=W1THENIFPEEK(S+2)=W2THENIFPEEK(S+3)=W3THEN540
535 S=S+256:GOTO 530
540 W=0
545 IFPEEK(S+W)<>46THENW=W+1:GOTO545
550 PRINTCHR$(147);
555 T=0
560 PRINT"0";
565 IFPEEK(S)=48THENPRINT:T=T+1:GOTO565
570 PRINTCHR$(147);
580 V=PEEK(65408)
590 IFV<>170ANDV<>0ANDV<>3THEN620
600 POKE53280,0:POKE53281,0:POKE646,5
610 C=13.5*4096
620 IFV<>22THEN650
630 POKE646,1
640 POKE36879,8
650 RETURN
700 PRINTCHR$(19)
710 FORI=1TOT-2:PRINTCHR$(29)"                                      "
720 POKEC+I*40+W-1,5:NEXT
730 FORI=0TOW-1:POKEC+I,5:NEXT
740 RETURN

READY.


